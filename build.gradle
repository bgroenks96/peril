/*
 * Copyright © 2011 - 2013 Aaron Mahan.
 * Copyright © 2013 - 2016 Forerunner Games, LLC.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

import groovy.json.JsonSlurper

buildscript {
  ant.get (src: "http://libgdx.badlogicgames.com/libgdx-site/service/getVersions?release=false", dest: "versions.json", ignoreerrors: "true", verbose: "on")
  def versionFile = file ("versions.json")
  def json
  if (versionFile.exists ()) {
    json = new JsonSlurper ().parseText (versionFile.text)
  } else throw new GradleException ("Unable to retrieve latest LibGDX versions, please check your internet connection.")

  ext {
    gdxVersion = json.libgdxSnapshot
    roboVMVersion = "2.2.0-SNAPSHOT"; //json.robovmVersion
    roboVMGradleVersion = "2.2.0-SNAPSHOT"; //json.robovmPluginVersion
    androidToolsVersion = json.androidBuildtoolsVersion
    androidSDKVersion = json.androidSDKVersion
    androidGradleToolsVersion = json.androidGradleToolVersion
    logbackClassicVersion = "1.1.3"
    jcommanderVersion = "1.48"
    fgToolsVersion = "1.0.1-SNAPSHOT"
  }

  repositories {
    jcenter ()
    mavenCentral ()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$androidGradleToolsVersion"
    classpath "com.mobidevelop.robovm:robovm-gradle-plugin:$roboVMGradleVersion"
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.11.3"
}

task wrapper (type: Wrapper) {
  gradleVersion = "2.13"
}

// Apply to parent project & all subprojects.
allprojects {
  apply plugin: "eclipse"
  apply plugin: "idea"
  apply plugin: "jacoco"

  group = projectGroup
  version = projectVersion

  ext {
    jacocoInstrumentedProjects = subprojects - project (":android") - project (":integration")
  }

  repositories {
    mavenLocal ()
    jcenter ()
    mavenCentral ()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "http://oss.jfrog.org/artifactory/oss-snapshot-local/" }
    maven { url "http://dl.bintray.com/forerunnergames/repository" }
  }
}

// Apply to all subprojects.
subprojects {
  // Allows disabling Android pre-dexing with -PdisablePreDex flag.
  // Can significantly speed up CI server builds.
  // See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
  project.plugins.whenPluginAdded { plugin ->
    if ("com.android.build.gradle.AppPlugin".equals (plugin.class.name)) {
      project.android.dexOptions.preDexLibraries = !project.hasProperty ("disablePreDex")
    } else if ("com.android.build.gradle.LibraryPlugin".equals (plugin.class.name)) {
      project.android.dexOptions.preDexLibraries = !project.hasProperty ("disablePreDex")
    }
  }

  eclipse.project.name = rootProject.name + "-" + project.name
}

// Apply to all subprojects except android.
configure (subprojects - project (":android")) {
  apply plugin: "java"

  [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:1.9.5"
    testRuntime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }

  test {
    beforeTest { desc ->
      logger.quiet ("\nRunning test: " + desc)
    }

    afterTest { desc, result ->
      if (result.resultType == TestResult.ResultType.FAILURE) {
        logger.quiet ("Failed test ${desc.name} [${desc.className}] with exception: ${result.exception}")
        if (result.exception != null) {
          result.exception.printStackTrace ()
        }
      }
    }

    testLogging.showStandardStreams = true;
  }
}

// Apply to all subprojects except android & integration.
configure (subprojects - project (":android") - project (":integration")) {
  sourceSets.main.java.srcDirs = ["src/main/java/"]
  sourceSets.main.resources.srcDirs = ["src/main/res/"]
  sourceSets.test.java.srcDirs = ["src/test/java/"]
  sourceSets.test.resources.srcDirs = ["src/test/res/"]
}

project (":desktop") {
  dependencies {
    compile project (":client")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    compile "com.beust:jcommander:$jcommanderVersion"
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":android") {
  apply plugin: "android"

  configurations { natives }

  dependencies {
    compile (project (":client")) {
      exclude module: "aws-java-sdk"
    }
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
    compile "org.slf4j:slf4j-android:1.7.13"
    compile "com.amazonaws:aws-android-sdk-s3:2.2.9"
  }
}

project (":ios") {
  apply plugin: "robovm"

  dependencies {
    compile project (":client")
    compile "com.mobidevelop.robovm:robovm-rt:${roboVMVersion}"
    compile "com.mobidevelop.robovm:robovm-cocoatouch:${roboVMVersion}"
    compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-ios"
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":common") {
  apply plugin: "groovy"

  dependencies {
    compile ("com.forerunnergames.tools:fg-tools-common:$fgToolsVersion")
    compile ("com.forerunnergames.tools:fg-tools-net:$fgToolsVersion")
    compile ("com.esotericsoftware:kryonet:2.22.0-RC1") {
      exclude module: "kryo"
    }
    compile "de.javakaffee:kryo-serializers:0.38"
    compile "net.engio:mbassador:1.2.4"
    compile "com.google.guava:guava:19.0"
    compile "com.google.code.findbugs:jsr305:3.0.1"
    compile "org.slf4j:slf4j-api:1.7.13"
  }
}

project (":core") {
  apply plugin: "groovy"

  dependencies {
    compile project (":common")
    compile ("com.stateforge:statemachine:3.6") {
      exclude module: "commons-io"
      exclude module: "junit"
    }
    testCompile project (":common").sourceSets.test.output
    testCompileOnly files { project (":common").sourceSets.test.java.srcDirs } // IDEA workaround
  }
}

project (":client") {
  dependencies {
    compile project (":common")
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.amazonaws:aws-java-sdk:1.10.44"
    testCompile project (":common").sourceSets.test.output
    testCompileOnly files { project (":common").sourceSets.test.java.srcDirs } // IDEA workaround
  }
}

project (":server") {
  dependencies {
    compile project (":core")
    compile "com.beust:jcommander:$jcommanderVersion"
    testCompile project (":common").sourceSets.test.output
    testCompileOnly files { project (":common").sourceSets.test.java.srcDirs } // IDEA workaround
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":integration") {
  configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
  }

  dependencies {
    testCompile project (":core")
    testCompile project (":server")
    testCompile project (":client")
    testCompile "org.testng:testng:6.9.10"
  }

  test {
    useTestNG ()
  }
}

task coverageReport (type: JacocoReport) {
  executionData fileTree (project.rootDir.absolutePath).include ("**/build/jacoco/*.exec")

  reports {
    xml.enabled true
    xml.destination "${buildDir}/reports/jacoco/jacoco.xml"
    html.enabled true
    html.destination "${buildDir}/reports/jacoco"
    csv.enabled false
  }

  jacocoInstrumentedProjects.each {
    sourceSets it.sourceSets.main
  }
}

coverageReport.dependsOn {
  jacocoInstrumentedProjects*.test
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
